import com.android.Version

buildscript {
	repositories {
		google()
		mavenCentral()
	}
	dependencies {
		// To be able to access com.android.Version from com.android.tools:common artifact.
		classpath(libs.agp)
	}
}

apply plugin: 'org.gradle.java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'

dependencies {
	def lintVersion = Version.ANDROID_TOOLS_BASE_VERSION
	compileOnly "com.android.tools.lint:lint-api:${lintVersion}"
	compileOnly "com.android.tools.lint:lint-checks:${lintVersion}"
}

ext.lintRegistry = { String fqcn ->
	jar {
		manifest {
			attributes("Lint-Registry-v2": fqcn)
		}
	}
}

java {
	toolchain {
		languageVersion = libs.versions.java.lint.map(JavaLanguageVersion::of)
	}
	tasks.withType(JavaCompile.class).configureEach { JavaCompile task ->
		task.options.compilerArgs += [
				"-Xlint:all",
				"-Werror",
		]
	}
}

project.kotlin {
	jvmToolchain(libs.versions.java.lint.get().toInteger())
}
