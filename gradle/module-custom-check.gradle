import com.android.builder.model.Version

buildscript {
	repositories {
		google()
		mavenCentral()
	}
	dependencies {
		// To be able to access com.android.builder.model.Version artifact.
		classpath "com.android.tools.build:builder-model:${libs.versions.agp.get()}"
	}
}

apply plugin: 'org.gradle.java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'

dependencies {
	def lintVersion = Version.ANDROID_TOOLS_BASE_VERSION
	compileOnly "com.android.tools.lint:lint-api:${lintVersion}"
	compileOnly "com.android.tools.lint:lint-checks:${lintVersion}"
}

ext.lintRegistry = { String fqcn ->
	jar {
		manifest {
			attributes("Lint-Registry-v2": fqcn)
		}
	}
}

java {
	toolchain {
		languageVersion = libs.versions.java.lint.map(JavaLanguageVersion::of)
	}
}

project.kotlin {
	jvmToolchain(libs.versions.java.lint.get().toInteger())
}
