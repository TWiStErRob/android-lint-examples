buildscript {
	ext.butterknife_version = '10.2.1'
	repositories {
		google()
		mavenCentral()
	}
	dependencies {
		classpath "com.jakewharton:butterknife-gradle-plugin:${butterknife_version}"
	}
}

apply from: rootProject.file('gradle/module-check-lib.gradle')
apply plugin: 'com.jakewharton.butterknife'

expectedViolationCount(2)

dependencies {
	implementation "com.jakewharton:butterknife:${butterknife_version}"
	annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife_version}"

	// A hack to get access to InvalidR2UsageDetector for source and debugging.
	afterEvaluate { debugCompileOnly(debugLintJars(project)) }
}

// Based on com.android.build.gradle.internal.dependency.VariantDependencies.computeArtifactCollection.
import com.android.build.gradle.internal.publishing.AndroidArtifacts

static FileCollection debugLintJars(Project project) {
	Configuration configuration = project.configurations.debugRuntimeClasspath
	Action<org.gradle.api.artifacts.ArtifactView.ViewConfiguration> view = { config ->
		config.attributes { container ->
			container.attribute(AndroidArtifacts.ARTIFACT_TYPE, AndroidArtifacts.ArtifactType.LINT.type)
		}
	}
	return configuration
			.getIncoming()
			.artifactView(view)
			.artifacts
			.artifactFiles
}
